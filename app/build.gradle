apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28

    kotlinOptions {
        jvmTarget = '1.6'
        freeCompilerArgs = ['-XXLanguage:+InlineClasses', '-Xallow-result-return-type']
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    defaultConfig {
        applicationId "com.minikorp.fakeshop"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    androidTestImplementation ('org.jetbrains.kotlin:kotlin-reflect:1.2.71') {
        force = true //To avoid conflicts in testing libs
    }

    //DI
    implementation 'org.kodein.di:kodein-di-generic-jvm:6.2.1'
    implementation 'org.kodein.di:kodein-di-framework-android-x:6.2.1'

    //Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.1'

    //AndroidX
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    def arch_version = "2.2.0-alpha01"
    implementation "androidx.lifecycle:lifecycle-extensions:$arch_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$arch_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$arch_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$arch_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$arch_version"

    def navigation = "2.1.0-alpha05"
    implementation "androidx.navigation:navigation-runtime-ktx:$navigation"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation"

    //Networking
    def retrofit_version = '2.5.1-SNAPSHOT'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation 'com.squareup.moshi:moshi-kotlin:1.8.0'

    //Debug
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //Testing
    def strikt_version = "0.20.1"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.3'
    testImplementation "io.strikt:strikt-core:$strikt_version"
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation "io.strikt:strikt-core:$strikt_version"
    def fragment_testing_version = '1.1.0-beta01'
    androidTestImplementation "androidx.fragment:fragment-testing:$fragment_testing_version"
    debugImplementation("androidx.fragment:fragment-testing:$fragment_testing_version") {
        //There is a bug in this library requiring androidx.test specific versions
        //that conflicts with androidTestImplementation deps.
        //It adds a custom activity so we have to se it as debugImplementation
        //But don't include any of it's additional dependencies.
        exclude group: 'androidx.test'
    }
}